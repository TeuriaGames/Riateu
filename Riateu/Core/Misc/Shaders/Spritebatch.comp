#version 450

struct ComputeData 
{
    vec2 position;
    vec2 scale;
    vec2 origin;
    vec2 uv[4];
    vec2 dimension;
    float rotation;
    float depth;
    vec4 color;
};

struct PositionTextureColorVertex 
{
    vec4 position;
    vec2 tex_coords;
    vec4 color;
};

layout (std430, set = 0, binding = 0) restrict readonly buffer inBuffer 
{
    ComputeData computeData[];
};

layout (std430, set = 1, binding = 0) restrict writeonly buffer outBuffer 
{
    PositionTextureColorVertex vertexData[];
};


layout (local_size_x = 64, local_size_y = 1, local_size_z = 1) in;
void main() 
{
    uint n = gl_GlobalInvocationID.x;
    ComputeData compData = computeData[n];

    vec2 scaledOrigin = compData.origin * compData.scale;

    mat4 origin = mat4(
        1, 0, 0, 0,
        0, 1, 0, 0,
        0, 0, 1, 0,
        -scaledOrigin.x, -scaledOrigin.y, 0., 1.
    );

    float val1 = cos(compData.rotation);
    float val2 = sin(compData.rotation);

    float x = (compData.position.x + scaledOrigin.x) * (1. - val1) + (compData.position.y + scaledOrigin.y) * val2;
    float y = (compData.position.y + scaledOrigin.y) * (1. - val1) - (compData.position.x + scaledOrigin.x) * val2;

    mat4 rotation = mat4(
        val1, val2, 0., 0.,
        -val2, val1, 0., 0.,
        x, y, 1., 0.,
        0., 0., 0., 1.
    );

    mat4 transform = origin * rotation;

    float width = compData.dimension.x * compData.scale.x;
    float height = compData.dimension.y * compData.scale.y;

    vec4 topLeft = vec4(compData.position.x, compData.position.y, compData.depth, 1.);
    vec4 topRight = vec4(compData.position.x + width, compData.position.y, compData.depth, 1.);
    vec4 bottomLeft = vec4(compData.position.x, compData.position.y + height, compData.depth, 1.);
    vec4 bottomRight = vec4(compData.position.x + width, compData.position.y + height, compData.depth, 1.);

    vertexData[n * 4u].position     = transform * topLeft;
    vertexData[n * 4u + 1].position = transform * topRight;
    vertexData[n * 4u + 2].position = transform * bottomLeft;
    vertexData[n * 4u + 3].position = transform * bottomRight;

    vertexData[n * 4u].tex_coords      = compData.uv[0];
    vertexData[n * 4u + 1u].tex_coords = compData.uv[1]; 
    vertexData[n * 4u + 2u].tex_coords = compData.uv[2];
    vertexData[n * 4u + 3u].tex_coords = compData.uv[3];

    vertexData[n * 4u].color = compData.color;
    vertexData[n * 4u + 1u].color = compData.color;
    vertexData[n * 4u + 2u].color = compData.color;
    vertexData[n * 4u + 3u].color = compData.color;
}
